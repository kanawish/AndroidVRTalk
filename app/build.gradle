apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

// All our 'non-public' properties are to be defined outside the project.
Properties props = new Properties()
// TODO: Replace with your own favorite directory.
props.load(new FileInputStream(file(System.getProperty("user.home") + "/.android/androidVrTalk.properties")))
// Extract needed properties from your local file.
def firebaseHost = props['FIREBASE_HOST'];
// TODO: Get keystore in repo, but keep passwords private and configured in local prop file.
// storePassword, keyAlias, keyPassword

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        applicationId "com.kanawish.androidvrtalk"

        minSdkVersion extMinSdkVersion
        targetSdkVersion extTargetSdkVersion

        versionCode extVersionCode
        versionName extVersionName

        buildConfigField "String", "FIREBASE_HOST", "\"${firebaseHost}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':shaderlib')
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.firebase:firebase-client-android:2.3.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
}
